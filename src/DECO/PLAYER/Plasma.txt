ACTOR BloxPlasmaRifle : PlasmaRifle replaces PlasmaRifle
{
	Weapon.BobStyle Alpha
	Weapon.BobSpeed 2.5
	Weapon.BobRangeX 0.2
	Weapon.BobRangeY 0.5
	Weapon.UpSound "weapons/plasmaup"
   	States
   	{
	Spawn:
		PLZP A -1
		Stop
	Ready:
		PLZG A 2 A_WeaponReady
		TNT1 A 0 A_PlaySound("weapons/plasmaloop", 7, 1, 1, 1.4)
		Loop
	Deselectbad:
		PLZG A 1 Offset(2, 34)
		PLZG A 1 Offset(7, 39)
		PLZG A 1 Offset(10, 47)
		PLZG A 1 Offset(22, 58)
		PLZG A 1 Offset(32, 69)
		PLZG A 1 Offset(54, 81)
		PLZG A 1 Offset(67, 100)
		TNT1 A 0 A_StopSound(7)
		PLZG A 1 A_Lower
		Wait 
   Deselect: 
      TNT1 AAAAAAAAAAAAAAAA 0 A_Lower
      TNT1 AAAAAAA 1 A_Lower
      Loop

	Select:
		PLZG A 1 Offset(67, 100)
		PLZG A 1 Offset(54, 81)
		PLZG A 1 Offset(32, 69)
		PLZG A 1 Offset(22, 58)
		PLZG A 1 Offset(10, 47)
		PLZG A 1 Offset(7, 39)
		PLZG A 1 Offset(2, 34)
		PLZG A 1 A_Raise	
		Wait
	Flash:
		TNT1 A 0 A_AlertMonsters
		PLZM A 1 Bright A_Light1
		PLZM B 2 Bright A_Light2
      	Goto LightDone
	Fire:
		TNT1 A 0 A_GiveInventory("hotToken",1)
		TNT1 A 0 A_PlaySound("weapons/plzstart", CHAN_WEAPON, 1.2, 0, 0.7)	
		TNT1 A 0 A_PlaySound("weapons/plzfire", CHAN_WEAPON, 0.90, 0, 0.7)
		TNT1 A 0 A_PlaySound("weapons/plzfire", 7, 0.05, 0, 0.20)
		TNT1 A 0 A_Gunflash
		PLZG A 1 Offset(0,36) a_firecustommissile ("hydrabullet",0,1,0,0,0,1)
		PLZG A 0 Offset(0,36) a_firecustommissile ("hydrabullet",-0.5,0,0,0,0,-0.5)
		PLZG A 0 Offset(0,36) a_firecustommissile ("hydrabullet",0.5,0,0,0,0,-0.5)
		PLZG A 0 Offset(0,38) a_setpitch(pitch-0.5)
		PLZG A 1 Offset(0,38) a_setpitch(pitch+0.3)
		PLZG A 1 Offset(0,37) a_setpitch(pitch+0.2)
		TNT1 A 0 A_ReFire("hotfire1")
		Goto Cooling
	Hold:
		TNT1 A 0 a_jump(255,"hotfire2")
		Goto Ready
	hotfire1:
		TNT1 A 0 A_GiveInventory("hotToken",1)
		TNT1 A 0 A_Gunflash
		TNT1 A 0 A_PlaySound("weapons/plzfire", CHAN_WEAPON, 0.90, 0, 0.7)
		TNT1 A 0 A_PlaySound("weapons/plzfire", 7, 0.05, 0, 0.20)
		PLZG A 1 Offset(0,36) a_firecustommissile ("hydrabullet",0,1,0,0,0,2)
		PLZG A 0 Offset(0,36) a_firecustommissile ("hydrabullet",-2,0,0,0,0,-1)
		PLZG A 0 Offset(0,36) a_firecustommissile ("hydrabullet",2,0,0,0,0,-1)
		PLZG A 0 Offset(0,38) a_setpitch(pitch-0.5)
		PLZG A 1 Offset(0,38) a_setpitch(pitch+0.3)
		PLZG A 1 Offset(0,37) a_setpitch(pitch+0.2)
		TNT1 A 0 A_ReFire("hotfire")
		TNT1 A 0 A_PlaySound("weapons/plzend", CHAN_6, 1.2, 0, 0.7)
		TNT1 A 0 A_JumpIf(CountInv("hotToken")<15,"Cooling")
		Goto Hot
	hotfire2:
		TNT1 A 0 A_GiveInventory("hotToken",1)
		TNT1 A 0 A_Gunflash
		TNT1 A 0 A_PlaySound("weapons/plzfire", CHAN_WEAPON, 0.90, 0, 0.7)
		TNT1 A 0 A_PlaySound("weapons/plzfire", 7, 0.05, 0, 0.20)
		PLZG A 1 Offset(0,36) a_firecustommissile ("hydrabullet",0,1,0,0,0,-2)
		PLZG A 0 Offset(0,36) a_firecustommissile ("hydrabullet",-2,0,0,0,0,1)
		PLZG A 0 Offset(0,36) a_firecustommissile ("hydrabullet",2,0,0,0,0,1)
		PLZG A 0 Offset(0,38) a_setpitch(pitch-0.5)
		PLZG A 1 Offset(0,38) a_setpitch(pitch+0.3)
		PLZG A 1 Offset(0,37) a_setpitch(pitch+0.2)
		TNT1 A 0 A_ReFire("hotfire1")
		TNT1 A 0 A_PlaySound("weapons/plzend", CHAN_6, 1.2, 0, 0.7)
		TNT1 A 0 A_JumpIf(CountInv("hotToken")<15,"Cooling")
		Goto Hot
	Hot:
		PLZO A 2 Offset(0,36)
		PLZO B 2 Offset(0,35)
		PLZO C 2 Offset(0,34)
		PLZO D 3 Offset(0,33)
		PLZO E 3 Offset(0,32)
		PLZO F 3
		PLZO G 3
		PLZO H 3
		PLZO I 4
		PLZO J 3
		PLZO K 2
		PLZO L 2
		PLZG A 2
		goto Cool
	Cooling:
		TNT1 A 0 A_WeaponReady
		TNT1 A 0 A_TakeInventory("hotToken",1)
		PLZG A 2 Offset(0,36)
		TNT1 A 0 A_TakeInventory("hotToken",1)
		TNT1 A 0 A_WeaponReady
		PLZG A 2 Offset(0,35)
		TNT1 A 0 A_TakeInventory("hotToken",1)
		TNT1 A 0 A_WeaponReady
		PLZG A 2 Offset(0,34)
		TNT1 A 0 A_TakeInventory("hotToken",2)
		TNT1 A 0 A_WeaponReady
		PLZG A 2 Offset(0,33)
		TNT1 A 0 A_TakeInventory("hotToken",3)
		TNT1 A 0 A_WeaponReady
		PLZG A 2 Offset(0,32)
	Cool:
		TNT1 A 0 A_TakeInventory("hotToken",15)
		TNT1 A 0 A_WeaponReady(WRF_NOBOB)
		Goto Ready
	}

}

actor hotToken : Inventory {
  +INVENTORY.UNTOSSABLE
  +INVENTORY.IGNORESKILL
  inventory.maxamount 15
}

actor hydrabullet : fastprojectile
{
	+notimefreeze
	projectile
	+THRUSPECIES
	species "friendly"
	// missiletype "hydratrail"
	missileheight 8
	+FORCEXYBILLBOARD
	+Bloodsplatter
	+LOOKALLAROUND
	+bright
	height 8
	radius 1
	speed 90
	damage 2
	scale 0.50
	decal doomimpscorch
	DeathSound "weapons/plasmax"
	states
	{
	spawn:
		HRBL A 1
		loop
	death:
		TNT1 A 0 A_SetScale(0.5)
		PLSE ABCDE 4 Bright
		stop
	}
}

actor hydratrail
{
	+NOINTERACTION
	+clientsideonly
	Radius 1
	Height 5
	Renderstyle add
	scale 0.50
	Alpha 0.85
	States
	{
	Spawn:
		TNT1 A 0
		HRBL A 1 Bright a_fadeout (0.50)
		wait
	}
}